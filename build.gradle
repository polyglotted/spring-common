apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.polyglotted'
version = '5.0.6.2'
sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://repo.spring.io/milestone" }
}
ext {
    attoParserVersion = '2.0.4.RELEASE' //locked by thymeleaf
    awsCommonVersion = '1.11.333'
    base64Version = '2.3.9'
    byteBuddyVersion = '1.7.9' //locked by springfox
    classmateVersion = '1.4.0'
    hamcrestVersion = '1.3'
    hibernateVersion = '6.0.10.Final'
    jasyptVersion = '1.9.2'
    jasyptSpringVersion = '2.0.0'
    javassistVersion = '3.22.0-GA'
    jbossLoggingVersion = '3.3.2.Final'
    junitVersion = '4.12'
    junitParamsVersion = '1.1.1'
    lombokVersion = '1.16.20'
    mapstructVersion = '1.2.0.Final' //locked by springfox
    ognlVersion = '3.1.12' //locked by thymeleaf
    reflectionsVersion = '0.9.11' //locked by springfox
    springVersion = '5.0.6.RELEASE'
    springBootVersion = '2.0.2.RELEASE'
    springFoxVersion = '2.8.0'
    springPluginVersion = '1.2.0.RELEASE' //locked by springfox
    springSecurityVersion = '5.0.5.RELEASE'
    swaggerVersion = '1.5.14' //locked by springfox
    tomcatVersion = '9.0.8'
    thymeleafVersion = '3.0.9.RELEASE'
    unbescapeVersion = '1.1.5.RELEASE' //locked by thymeleaf
    utilsCommonVersion = '1.0.24'
    validationApiVersion = '2.0.1.Final'
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "io.polyglotted:utils-common:${utilsCommonVersion}"
    compileOnly "io.polyglotted:aws-common:${awsCommonVersion}"

    compile "com.fasterxml:classmate:${classmateVersion}"
    compile("com.github.ulisesbocchio:jasypt-spring-boot:${jasyptSpringVersion}") {
        exclude group: "org.jasypt", module: "jasypt"
    }
    compile "javax.validation:validation-api:${validationApiVersion}"
    compile "net.iharder:base64:${base64Version}"
    compile("ognl:ognl:${ognlVersion}") {
        exclude group: "org.javassist", module: "javassist"
    }
    compile "org.apache.tomcat:tomcat-annotations-api:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
    compile("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}") {
        exclude group: "org.apache.tomcat", module: "tomcat-annotations-api"
    }
    compile("org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}") {
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-core"
    }
    compile "org.attoparser:attoparser:${attoParserVersion}"
    compile("org.hibernate.validator:hibernate-validator:${hibernateVersion}") {
        exclude group: "com.fasterxml", module: "classmate"
        exclude group: "javax.validation", module: "validation-api"
        exclude group: "org.jboss.logging", module: "jboss-logging"
    }
    compile "org.jasypt:jasypt:${jasyptVersion}"
    compile "org.javassist:javassist:${javassistVersion}"
    compile "org.jboss.logging:jboss-logging:${jbossLoggingVersion}"
    compile "org.springframework:spring-jcl:${springVersion}"
    compile("org.springframework:spring-core:${springVersion}") {
        exclude group: "org.springframework", module: "spring-jcl"
    }
    compile("org.springframework:spring-beans:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
    }
    compile("org.springframework:spring-expression:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
    }
    compile("org.springframework:spring-aop:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
    }
    compile("org.springframework:spring-tx:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
    }
    compile("org.springframework:spring-web:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
    }
    compile("org.springframework:spring-context:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-aop"
    }
    compile("org.springframework:spring-context-support:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-context"
    }
    compile("org.springframework:spring-webmvc:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-web"
        exclude group: "org.springframework", module: "spring-context"
    }
    compile("org.springframework.security:spring-security-core:${springSecurityVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-context"
    }
    compile("org.springframework.security:spring-security-config:${springSecurityVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework.security", module: "spring-security-core"
    }
    compile("org.springframework.security:spring-security-web:${springSecurityVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-web"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework.security", module: "spring-security-core"
    }
    compile("org.thymeleaf:thymeleaf:${thymeleafVersion}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "ognl", module: "ognl"
        exclude group: "org.attoparser", module: "attoparser"
        exclude group: "org.unbescape", module: "unbescape"
    }
    compile("org.thymeleaf:thymeleaf-spring5:${thymeleafVersion}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.thymeleaf", module: "thymeleaf"
    }
    compile("org.springframework.boot:spring-boot:${springBootVersion}") {
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-context"
    }
    compile("org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}") {
        exclude group: "org.springframework.boot", module: "spring-boot"
    }
    compile("org.springframework.boot:spring-boot-actuator:${springBootVersion}") {
        exclude group: "org.springframework.boot", module: "spring-boot"
    }
    compile("org.springframework.boot:spring-boot-actuator-autoconfigure:${springBootVersion}") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jsr310"
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework.boot", module: "spring-boot"
        exclude group: "org.springframework.boot", module: "spring-boot-autoconfigure"
        exclude group: "org.springframework.boot", module: "spring-boot-actuator"
    }
    compile "org.unbescape:unbescape:${unbescapeVersion}"

    compile("io.springfox:springfox-core:${springFoxVersion}") {
        exclude group: "org.springframework.plugin", module: "spring-plugin-metadata"
        exclude group: "org.springframework.plugin", module: "spring-plugin-core"
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.fasterxml", module: "classmate"
        exclude group: "net.bytebuddy", module: "byte-buddy"
    }
    compile("io.springfox:springfox-spi:${springFoxVersion}") {
        exclude group: "io.springfox", module: "springfox-core"
    }
    compile("io.springfox:springfox-schema:${springFoxVersion}") {
        exclude group: "io.springfox", module: "springfox-core"
        exclude group: "io.springfox", module: "springfox-spi"
    }
    compile("io.springfox:springfox-spring-web:${springFoxVersion}") {
        exclude group: "io.springfox", module: "springfox-spi"
        exclude group: "org.springframework.plugin", module: "spring-plugin-metadata"
        exclude group: "org.springframework.plugin", module: "spring-plugin-core"
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.reflections", module: "reflections"
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.fasterxml", module: "classmate"
    }
    compile("io.springfox:springfox-swagger-common:${springFoxVersion}") {
        exclude group: "io.swagger", module: "swagger-models"
        exclude group: "io.springfox", module: "springfox-spring-web"
        exclude group: "io.springfox", module: "springfox-schema"
        exclude group: "io.springfox", module: "springfox-spi"
        exclude group: "org.springframework.plugin", module: "spring-plugin-metadata"
        exclude group: "org.springframework.plugin", module: "spring-plugin-core"
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.fasterxml", module: "classmate"
        exclude group: "io.swagger", module: "swagger-annotations"
    }
    compile("io.springfox:springfox-swagger2:${springFoxVersion}") {
        exclude group: "io.springfox", module: "springfox-swagger-common"
        exclude group: "io.swagger", module: "swagger-models"
        exclude group: "io.springfox", module: "springfox-spring-web"
        exclude group: "io.springfox", module: "springfox-schema"
        exclude group: "io.springfox", module: "springfox-spi"
        exclude group: "org.springframework.plugin", module: "spring-plugin-metadata"
        exclude group: "org.springframework.plugin", module: "spring-plugin-core"
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "com.google.guava", module: "guava"
        exclude group: "com.fasterxml", module: "classmate"
        exclude group: "io.swagger", module: "swagger-annotations"
        exclude group: "org.mapstruct", module: "mapstruct"
    }
    compile("io.springfox:springfox-swagger-ui:${springFoxVersion}") {
        exclude group: "io.springfox", module: "springfox-spring-web"
    }
    compile "io.swagger:swagger-annotations:${swaggerVersion}"
    compile("io.swagger:swagger-models:${swaggerVersion}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "io.swagger", module: "swagger-annotations"
    }
    compile "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
    compile "org.mapstruct:mapstruct:${mapstructVersion}"
    compile("org.reflections:reflections:${reflectionsVersion}") {
        exclude group: "com.google.guava", module: "guava"
        exclude group: "org.javassist", module: "javassist"
    }
    compile("org.springframework.plugin:spring-plugin-core:${springPluginVersion}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework", module: "spring-aop"
        exclude group: "org.springframework", module: "spring-beans"
    }
    compile("org.springframework.plugin:spring-plugin-metadata:${springPluginVersion}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.springframework.plugin", module: "spring-plugin-core"
    }

    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile("junit:junit:${junitVersion}") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }
    testCompile("pl.pragmatists:JUnitParams:${junitParamsVersion}") {
        exclude group: "junit", module: "junit"
    }
    testCompile("org.springframework:spring-test:${springVersion}") {
        exclude group: "org.springframework", module: "spring-core"
    }
    testCompile("org.springframework.boot:spring-boot-test:${springBootVersion}") {
        exclude group: "org.springframework.boot", module: "spring-boot"
    }
    testCompile("org.springframework.boot:spring-boot-test-autoconfigure:${springBootVersion}") {
        exclude group: "org.springframework.boot", module: "spring-boot-autoconfigure"
        exclude group: "org.springframework.boot", module: "spring-boot-test"
    }
}
sourceSets {
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}

test {
    systemProperty "jasypt.encryptor.password", project.getProperty("jasypt.encryptor.password")
}
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'spring-common'
                packaging 'jar'
                description 'Polyglotted Common Utils'

                url 'https://github.com/polyglotted/spring-common'
                scm {
                    connection 'scm:git:git@github.com:polyglotted/spring-common.git'
                    developerConnection 'scm:git:git@github.com:polyglotted/spring-common.git'
                    url 'https://github.com/polyglotted/spring-common'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'vshank77'
                        name 'Shankar Vasduevan'
                        email 'shankar [at] polyglotted [dot] io'
                    }
                }
            }
        }
    }
}